
"""

To run the full pipeline, submit the following line from within the
same directory as the Snakefile while on the head node (the paths to
the data files are relative to the Snakefile):

nohup snakemake -kps Snakefile-population -j 96 --ri -c "qsub -l h_vmem={params.h_vmem} -l bigio={params.bigio} -N {params.name} -V -j y -cwd -o {log}" &

"""

from Bio import SeqIO

###############################################################################
# SOFTWARE
###############################################################################
FASTQC='/mnt/lustre/data/tools/FastQC/'
UMITOOLS='/mnt/lustre/home/jdblischak/programs/virtualenv-1.11/py2.7/bin/'
SAMTOOLS = '/usr/local/bin/'
SUBREAD = '/home/jdblischak/src/subread-1.4.4-Linux-x86_64/bin/'

###############################################################################
# Data
###############################################################################
DATA_DIR = 'data/population/'
LOG_DIR = 'log/'
REF_GENOME = 'data/genome/combined' # prefix only

###############################################################################
# Target rules
###############################################################################

localrules: all, qc

samples = ['19239_yale']

rule all:
	input: expand(DATA_DIR + '{seq}.counts.txt', seq = samples)

rule qc:
	input: expand(DATA_DIR + '{seq}_fastqc.zip', seq = samples)

###############################################################################
# Per-fastq processing: from raw reads to gene counts
###############################################################################

rule unzip:
	input: DATA_DIR + '{seq}.fastq.gz'
	output: temp(DATA_DIR + '{seq}.fastq')
	message: 'Unzipping sample {input}'
	params: h_vmem = '8g', bigio = '0',
	        name = lambda wildcards: 'unzip.' + wildcards.seq
	log: LOG_DIR
	shell: 'zcat {input} > {output}'

rule fastqc:
	input: DATA_DIR + '{seq}.fastq'
	output: DATA_DIR + '{seq}_fastqc.zip'
	message: 'Running FastQC on sample {input}'
	params: h_vmem = '8g', bigio = '0',
	        name = lambda wildcards: 'fastqc.' + wildcards.seq
	log: LOG_DIR
	shell: '{FASTQC}fastqc {input}'

rule map:
	input: fastq = DATA_DIR + '{seq}.fastq',
               genome = REF_GENOME + '.reads'
	output: temp(DATA_DIR + '{seq}.bam')
	message: 'Map reads of sample {input.fastq}'
	params: h_vmem = '12g', bigio = '1',
	        name = lambda wildcards: 'map.' + wildcards.seq
	log: LOG_DIR
	shell: '{SUBREAD}subread-align -i {REF_GENOME} -r {input.fastq} --BAMoutput > {output}'

rule sort_bam:
	input: DATA_DIR + '{seq}.bam'
	output: DATA_DIR + '{seq}.sorted.bam'
	message: 'Sort bam file {input}'
	params: h_vmem = '8g', bigio = '1',
	        name = lambda wildcards: 'sort_bam.' + wildcards.seq,
                prefix = lambda wildcards: DATA_DIR + wildcards.seq + '.sorted'
	log: LOG_DIR
	shell: '{SAMTOOLS}samtools sort {input} {params.prefix}'

rule index_bam:
	input: DATA_DIR + '{seq}.sorted.bam'
	output: DATA_DIR + '{seq}.sorted.bam.bai'
	message: 'Index sorted bam file {input}'
	params: h_vmem = '8g', bigio = '0',
	        name = lambda wildcards: 'index_bam.' + wildcards.seq
	log: LOG_DIR
	shell: '{SAMTOOLS}samtools index {input}'

rule download_exons:
	output: 'data/genome/exons.saf'
	message: 'Create SAF file of human exons.'
	params: h_vmem = '8g', bigio = '0',
	        name = 'download_exons'
	log: LOG_DIR
	shell: 'Rscript download_exons.R > {output}'

rule featureCounts:
	input: reads = DATA_DIR + '{seq}.sorted.bam',
               anno = 'data/genome/exons_ERCC92.saf'
	output: counts = DATA_DIR + '{seq}.counts.txt',
                summary = DATA_DIR + '{seq}.counts.txt.summary'
	message: 'Counts number of reads per feature for {input.reads}.'
	params: h_vmem = '8g', bigio = '1',
	        name = lambda wildcards: 'featureCounts.' + wildcards.seq
	log: LOG_DIR
	shell: '{SUBREAD}featureCounts -a {input.anno} -F SAF -o {output.counts} {input.reads}'
