
"""

To run the full pipeline, submit the following line from within the
same directory as the Snakefile while on the head node (the paths to
the data files are relative to the Snakefile):

nohup snakemake -kps Snakefile-population -j 96 --ri -c "qsub -l h_vmem={params.h_vmem} -l bigio={params.bigio} -N {params.name} -V -j y -cwd -o {log}" &

"""

from Bio import SeqIO

###############################################################################
# SOFTWARE
###############################################################################
FASTQC='/mnt/lustre/data/tools/FastQC/'
UMITOOLS='/mnt/lustre/home/jdblischak/programs/virtualenv-1.11/py2.7/bin/'
SAMTOOLS = '/usr/local/bin/'
SUBREAD = '/home/jdblischak/src/subread-1.4.4-Linux-x86_64/bin/'

###############################################################################
# Data
###############################################################################
DATA_DIR = 'data/'
POP_DIR = DATA_DIR + 'population/'
LOG_DIR = 'log/'
REF_GENOME = DATA_DIR + '/genome/combined' # prefix only

###############################################################################
# Target rules
###############################################################################

localrules: all, qc

samples = ['NA19239_yale']

rule all:
	input: expand(POP_DIR + '{seq}.counts.txt', seq = samples)

rule qc:
	input: expand(POP_DIR + '{seq}_fastqc.zip', seq = samples)

###############################################################################
# Per-fastq processing: from raw reads to gene counts
###############################################################################

rule unzip:
	input: POP_DIR + '{seq}.fastq.gz'
	output: temp(POP_DIR + '{seq}.fastq')
	message: 'Unzipping sample {input}'
	params: h_vmem = '8g', bigio = '0',
	        name = lambda wildcards: 'unzip.' + wildcards.seq
	log: LOG_DIR
	shell: 'zcat {input} > {output}'

rule fastqc:
	input: POP_DIR + '{seq}.fastq'
	output: POP_DIR + '{seq}_fastqc.zip'
	message: 'Running FastQC on sample {input}'
	params: h_vmem = '8g', bigio = '0',
	        name = lambda wildcards: 'fastqc.' + wildcards.seq
	log: LOG_DIR
	shell: '{FASTQC}fastqc {input}'

rule map:
	input: fastq = POP_DIR + '{seq}.fastq',
               genome = REF_GENOME + '.reads'
	output: temp(POP_DIR + '{seq}.bam')
	message: 'Map reads of sample {input.fastq}'
	params: h_vmem = '12g', bigio = '1',
	        name = lambda wildcards: 'map.' + wildcards.seq
	log: LOG_DIR
	shell: '{SUBREAD}subread-align -i {REF_GENOME} -r {input.fastq} --BAMoutput > {output}'

rule sort_bam:
	input: POP_DIR + '{seq}.bam'
	output: POP_DIR + '{seq}.sorted.bam'
	message: 'Sort bam file {input}'
	params: h_vmem = '8g', bigio = '1',
	        name = lambda wildcards: 'sort_bam.' + wildcards.seq,
                prefix = lambda wildcards: POP_DIR + wildcards.seq + '.sorted'
	log: LOG_DIR
	shell: '{SAMTOOLS}samtools sort {input} {params.prefix}'

rule index_bam:
	input: POP_DIR + '{seq}.sorted.bam'
	output: POP_DIR + '{seq}.sorted.bam.bai'
	message: 'Index sorted bam file {input}'
	params: h_vmem = '8g', bigio = '0',
	        name = lambda wildcards: 'index_bam.' + wildcards.seq
	log: LOG_DIR
	shell: '{SAMTOOLS}samtools index {input}'

rule featureCounts:
	input: reads = POP_DIR + '{seq}.sorted.bam',
               anno = DATA_DIR + 'genome/exons_ERCC92.saf'
	output: counts = POP_DIR + '{seq}.counts.txt',
                summary = POP_DIR + '{seq}.counts.txt.summary'
	message: 'Counts number of reads per feature for {input.reads}.'
	params: h_vmem = '8g', bigio = '1',
	        name = lambda wildcards: 'featureCounts.' + wildcards.seq
	log: LOG_DIR
	shell: '{SUBREAD}featureCounts -a {input.anno} -F SAF -o {output.counts} {input.reads}'
